#!/usr/bin/perl

#  mkd64 -- Create D64 image from list of files.
#  Copyright (C) Dieter Baron
#
#  This file is part of Zak Supervisor, a Music Monitor for the Commodore 64.
#  The authors can be contacted at <zak-supervisor@tpau.group>.
#
#  Redistribution and use in source and binary forms, with or without
#  modification, are permitted provided that the following conditions
#  are met:
#  1. Redistributions of source code must retain the above copyright
#     notice, this list of conditions and the following disclaimer.
#  2. The names of the authors may not be used to endorse or promote
#     products derived from this software without specific prior
#     written permission.
#
#  THIS SOFTWARE IS PROVIDED BY THE AUTHORS ``AS IS'' AND ANY EXPRESS
#  OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
#  WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
#  ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHORS BE LIABLE FOR ANY
#  DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
#  DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE
#  GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
#  INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER
#  IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
#  OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN
#  IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

use strict;
use warnings;

main();

sub main {
    if (scalar(@ARGV) != 3) {
        usage();
        exit(1);
    }

    my ($image, $name, $listfile) = @ARGV;

    unless (process($image, $name, $listfile)) {
        unlink($image);
        exit(1);
    }

    exit(0);
}

sub process {
    my ($image, $disk_name, $listfile) = @_;

    my $depfd;
    unless (open($depfd, ">", "$image.d")) {
        error("can't create depfile");
        return undef;
    }
    print $depfd "$image:";

    my $src_dir = $listfile;
    $src_dir =~ s,/[^/]*$,,;
    my $build_dir = $image;
    $build_dir =~ s,/[^//]*$,,;

    my $type = "\L$image";
    $type =~ s/.*\.//;

    my $fh;

    unless (open($fh, '<', $listfile)) {
        error("can't open list file '$listfile': $!");
        return undef;
    }

    unless (checked_system('c1541', '-silent', '-format', $disk_name, $type, $image)) {
        return undef;
    }

    while (my $line = <$fh>) {
        chomp $line;
        my ($name, $file);

        if ($line =~ m/(.*)\t(.*)/) {
            ($file, $name) = ($1, $2);
        }
        else {
            $file = $line;
            $name = $file;
            $name =~ s/\.prg$//;
        }
        if (-f "$build_dir/$file") {
            $file = "$build_dir/$file";
        }
        elsif (-f "$src_dir/$file") {
            $file = "$src_dir/$file";
        }
        else {
            error("can't find '$file' in '$build_dir' or '$src_dir'");
            return undef;
        }
        print $depfd " $file";

        unless (checked_system('c1541', $image, '-silent', '-write', $file, $name)) {
            return undef;
        }
    }

    print $depfd "\n";
    close($depfd);

    return 1;
}

sub usage {
    print STDERR "usage: $0 file.d64 'name,id' list-file\n";
}

sub error {
    my ($message) = @_;

    print STDERR "$0: $message\n";
}

sub warning {
    my ($message) = @_;

    print STDERR "$0: warning: $message\n";
}

sub checked_system {
    unless (system(@_) == 0) {
        error("can't execute '$_[0]': $?");
        return undef;
    }
    return 1;
}
